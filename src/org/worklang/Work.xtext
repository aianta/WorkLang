grammar org.worklang.Work with org.eclipse.xtext.common.Terminals

generate work "http://www.worklang.org/Work"


Model:
	fields += FieldDefinition*
	experts += ExpertDefinition*
	practitioners += Practitioner*
	instructions += Instruction*
;

/* Executable Structure
 * Below are the grammar rules for defining execution requests for the work interpreter.
 */
Instruction:
	objectType = 'instruction' space = [Space] transition = [TransitionID]
;


UseDefinition:
	use = Use predefinedValue = [StateID] field = FromFieldDefinition
;


Practitioner:
	practitionerDef = 'practitioner'
	user = User
	'{'
	knowledgeDef = KnowledgeDefinition?
	skillsDef = SkillsDefinition?
	'}'
;

ExpertDefinition:
	expertDef = 'expert' user = User
	'{'
	domainDef = DomainDefinition?
	knowledgeDef = KnowledgeDefinition?
	skillsDef = SkillsDefinition?
	'hourlyRate'
	'$' rate =  INT
	'}'
;

User:
	name = ID
;
	

DomainDefinition:
	domain = 'domain'
	'{'
		domainAreas += ReferenceField*
	'}'
;

KnowledgeDefinition:
	knowledgeDef = 'knowledge''{'
		knowledge += ReferenceState*
	'}'
;

SkillsDefinition:
	skillsDef = 'skills''{'
		skills += ReferenceTransition*
	'}'
;


FieldDefinition:
	field = Field
	space = Space
	extends=(ExtendsDefinition)? 
	'{'
	(conceptualspace = Namespace)?
	(referencespace = Referencespace)?
	(instancespace = Instancespace)?
	'}'	
;

Space:
	name = ID
;

Namespace:
	spaceType = Definitions
	'{'
	states += StateObjectDefinition*
	transitions += TransitionObjectDefinition*
	'}'
;

Referencespace:
	spaceType = References
	'{'
	refFields += ReferenceField*
	refStates += ReferenceState*
	refTransition += ReferenceTransition*
	'}'
;

Instancespace:
	spaceType  = Instances
	'{'
		instanceStates += Instance*
	'}'
;

ReferenceField:
	ref = Field 
	space = Space
;

StateObjectDefinition:
	(type = Primitive| type = Compound)
	state = StateDefinition 
;

TransitionObjectDefinition:
	(type = Primitive| type = Compound)
	transition = TransitionDefinition
;


StateDefinition:
	object = State state = StateID (compoundStateDef= WithStatesDefinition)?
;


StateID:
	 name = ID
;

WithStatesDefinition:
	composition = With
	'{'
	states= Predicate
	'}'
;

WithTransitionsDefinition:
	composition = With
	'{'
	transitions = TransitionalExpression
	'}'
;

ReferenceState:
	object = State
 	refState = [StateID]
	from = FromFieldDefinition 
	(('called'|'as')
		state = StateID)?
	(composition = WithStatesDefinition)?
	
;

TransitionDefinition:
	object = Transition transition = TransitionID (in = InputDefinition)? out = OutputDefinition (composition = WithTransitionsDefinition)?
;

TransitionID:
	name = ID
;

ReferenceTransition:
	object = Transition
	refTransition = [TransitionID]
	fromDef = FromFieldDefinition
	(('called'|'as')
		transition = TransitionID)?
	(composition = WithTransitionsDefinition)?
	
;

FromFieldDefinition:
	from = From
	refSpace = [Space]
;


InputDefinition:
	input = Input inputState += [StateID]*
;

OutputDefinition:
	output = Output outputState = [StateID]
;

ExtendsDefinition:
	type = Extends
	namespaces += [Space]+	
;


Primitive:
	'primitive' 
;

Compound:
	'compound'
;

Field:
	'field'
;

State:
	'state'
;


Transition:
	'transition'
;

Definitions:
	'definitions'
;

References:
	'references'
;

Instances:
	'instances'
;

Input:
	'input'
;

Output:
	'output'
;

From:
	'from'
;

With:
	'with'
;

Extends:
	'extends'
;

NotOp:
	'NOT'
;

AndOp:
	'AND'
;

OrOp:
	'OR'
;

Set:
	'set'
;

To:
	'to'
;

Use:
	'use'
;


Count:
	INT
;

Instance:
	instanceDef = 'instance' 
	(transitionDeclaration = TransitionDeclaration| stateDeclaration = StateDeclaration) 
	name = STRING 
	(state = StateInstance | transition = TransitionInstance)
;

TransitionDeclaration:
	objectType = Transition transition = TransitionID
;

StateDeclaration:
	objectType = State state = StateID
;

TransitionInstance:
	host = STRING
	port = INT
	(path = STRING)?
;

StateInstance:
	 
	'{'
		members += (SetStatement| UseDefinition| Instance)+
	'}'
			
;

SetStatement:
	statement = Set variable = [StateID] toDef = ToDefinition
;

ToDefinition:
	to = To value = STRING
;

//State Composition Expression Rules
Predicate:
	(negation?=NotOp)? expression = BinaryExpression
;

BinaryExpression:
	TerminalBinaryExpression ({Operation.left=current}
		op= (AndOp|OrOp) right = TerminalBinaryExpression)*
;

TerminalBinaryExpression returns BinaryExpression:
	'(' Predicate ')'|
	 {StateID} value = [StateID]|
	 {StateID}(listDef = 'setOf' value = [StateID])
;


//Transition Composition Expression Rules
TransitionalExpression:
	TerminalTransitionalExpression ({Operation.left=current}
		op= 'THEN' right = TerminalTransitionalExpression)*	
;

TerminalTransitionalExpression returns TransitionalExpression:
	'(' TransitionalExpression ')'|
	{TransitionID} value = [TransitionID]
;




